plugins {
	id 'org.springframework.boot' version '2.2.7.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
}

group = 'com.peercoin'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
	flatDir {
		dirs 'lib'
	}
}

task copyCore(type:Copy) {
	from('../core/build/libs') {
		include '*.jar'
	}
	from('../integration/build/libs') {
		include '*.jar'
	}
	into 'lib/'
}
task copyPlugins(type:Copy) {
	from('../plugins') {
		include '*.py'
	}
	from('../config/web') {
		include '*'
	}
	into 'src/main/resources'
}

task copyPluginDeps(type:Copy) {
	from ('../plugins/lib') {
		include '*/**'
	}
	into 'src/main/resources'
}

task removePlugins(type: Delete) {
	delete fileTree('src/main/resources') {
		include '*/**'
		exclude 'static/'
		exclude 'templates/'
		exclude '*.properties*'
	}
}

clean.dependsOn removePlugins
copyPluginDeps.dependsOn removePlugins
copyPlugins.dependsOn copyPluginDeps
compileJava.dependsOn copyCore
compileJava.dependsOn copyPlugins

dependencies {
	implementation project(':core')
	implementation name: 'core-0.0.1-SNAPSHOT'
	implementation project(':integration')
	implementation name: 'integration-1.0-SNAPSHOT-all'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.apache.commons:commons-lang3'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.mockito:mockito-all:1.10.19'
}

test {
	useJUnitPlatform()
}
